/*
 *
 *  MIT License
 *
 *  (C) Copyright 2021-2025 Hewlett Packard Enterprise Development LP
 *
 *  Permission is hereby granted, free of charge, to any person obtaining a
 *  copy of this software and associated documentation files (the "Software"),
 *  to deal in the Software without restriction, including without limitation
 *  the rights to use, copy, modify, merge, publish, distribute, sublicense,
 *  and/or sell copies of the Software, and to permit persons to whom the
 *  Software is furnished to do so, subject to the following conditions:
 *
 *  The above copyright notice and this permission notice shall be included
 *  in all copies or substantial portions of the Software.
 *
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
 *  THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
 *  OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
 *  ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
 *  OTHER DEALINGS IN THE SOFTWARE.
 *
 */
@Library('cms-meta-tools@develop') _
@Library('csm-shared-library') __

def pyImage = 'artifactory.algol60.net/csm-docker/stable/csm-docker-sle-python'
def sleImage = 'artifactory.algol60.net/csm-docker/stable/csm-docker-sle'

def buildRootReldir() {
    return "dist/rpmbuild"
}

def getSleVersion() {
    return sh(returnStdout: true, script: 'awk -F= \'/VERSION_ID/{gsub(/["]/,""); print \$NF}\' /etc/os-release').trim()
}

def getRpmName(packageName, pythonVersion) {
    if (pythonVersion == "meta") {
        return "${packageName}"
    }
    // If it is not meta, then it should be 'major.minor'
    if (!isValidMajorDotMinor(pythonVersion)) {
        error("Invalid Python version specified: ${pythonVersion}")
    }
    if (pythonVersion == "3.6") {
        return "python3-${packageName}"
    }
    def pythonVersionParts = pythonVersion.tokenize('.')
    def pyMajor = "${pythonVersionParts[0]}"
    def pyMinor = "${pythonVersionParts[1]}"
    return "python${pyMajor}${pyMinor}-${packageName}"
}

def buildRelDir(rpmName, rpmOs, rpmArch) {
    def build_root_reldir = buildRootReldir()
    return "${build_root_reldir}/${rpmArch}/${rpmOs}/${rpmName}"
}

def publishRpms(rpmName, rpmOs, rpmArch, buildReldir) {
    sh "ls -R ${buildReldir}"
    echo "${rpmName} ${rpmOs} ${rpmArch}"
    publishCsmRpms(
        arch: "${rpmArch}",
        component: "${rpmName}",
        isStable: env.IS_STABLE,
        os: "${rpmOs}",
        pattern: "${buildReldir}/RPMS/${rpmArch}/*.rpm",
    )
    publishCsmRpms(
        arch: "src",
        component: "${rpmName}",
        isStable: env.IS_STABLE,
        os: "${rpmOs}",
        pattern: "${buildReldir}/SRPMS/*.rpm",
    )
}

pipeline {
    agent {
        label "metal-gcp-builder"
    }

    options {
        buildDiscarder(logRotator(numToKeepStr: "10"))
        disableConcurrentBuilds()
        timeout(time: 90, unit: 'MINUTES')
        timestamps()
    }

    environment {
        NAME = "bos-reporter"
        DESCRIPTION = "Configuration Framework Service Trust Environment"
        IS_STABLE = getBuildIsStable()
        PYMOD_BUILD_PY_VERSION = "3.12"
        BUILD_METADATA = getRpmRevision(isStable: env.IS_STABLE)
        BUILD_ROOT_RELDIR = buildRootReldir()
        META_RPM_OS = "noos"
        META_RPM_NAME = getRpmName(env.NAME, "meta")
        META_RPM_ARCH = "noarch"
        META_RPM_SPEC_FILE = "${env.NAME}.spec"
        META_BUILD_RELDIR = buildRelDir(env.META_RPM_NAME, env.META_RPM_OS, env.META_RPM_ARCH)
    }

    stages {
        stage("Init") {
            steps {
                // This function is defined in cms-meta-tools:vars/cloneCMSMetaTools.groovy
                cloneCMSMetaTools()

                // This function is defined in cms-meta-tools:vars/setVersionFiles.groovy
                setVersionFiles()

                withCredentials([usernamePassword(credentialsId: 'artifactory-algol60-readonly', passwordVariable: 'ARTIFACTORY_PASSWORD', usernameVariable: 'ARTIFACTORY_USERNAME')]) {
                    sh "make runbuildprep"
                }
            }
        }

        stage ("Prep") {
            steps {
                sh "make lint"

                // The RPM build metadata can be added outside of the matrix, because it is just based on the
                // Git commit being built
                echo "RPM build metadata is ${env.BUILD_METADATA}"
                runLibraryScript("addRpmMetaData.sh", "${env.WORKSPACE}/bos-reporter.spec")
                runLibraryScript("addRpmMetaData.sh", "${env.WORKSPACE}/python-bos-reporter.spec")

                // Just using this as a means to generate the netrc file that we need
                getDockerBuildArgs()

                sh "make rpm_pre_clean"
            }
        }

        stage("Build Python module") {
            agent {
                docker {
                    args '-v /home/jenkins/.ssh:/home/jenkins/.ssh -v /home/jenkins/.netrc:/home/jenkins/.netrc'
                    reuseNode true
                    image "${pyImage}:${PYMOD_BUILD_PY_VERSION}"
                }
            }
            environment {
                PY_VERSION = "${PYMOD_BUILD_PY_VERSION}"
                PY_PATH = getPythonBinaryPath(env.PYMOD_BUILD_PY_VERSION)
            }
            steps {
                sh "make pymod_build"
            }
        }

        stage('Pylint') {
            matrix {
                axes {
                    axis {
                        name 'PY_VERSION'
                        values '3.6', '3.9', '3.10', '3.11', '3.12'
                    }
                }
                agent {
                    docker {
                        args '-v /home/jenkins/.ssh:/home/jenkins/.ssh -v /home/jenkins/.netrc:/home/jenkins/.netrc'
                        reuseNode true
                        image "${pyImage}:${PY_VERSION}"
                    }
                }
                environment {
                    PY_PATH = getPythonBinaryPath(env.PY_VERSION)
                }
                stages {
                    stage("Pylint") {
                        steps {
                            sh "make pymod_pylint_setup"
                            sh "make pymod_pylint_errors"
                            sh "make pymod_pylint_full"
                        }
                    }
                }
            }
        }
        stage('Build & Publish Python RPMs') {
            matrix {
                axes {
                    axis {
                        name 'DOCKER_TAG'
                        // $pyImage:$PY_VERSION implies the SLES version used for the build
                        // $pyImage:$PY_VERSION-SLES15.5 dictates the python version and SLES version
                        values '3.6', '3.9', '3.10', '3.11-SLES15.5', '3.11-SLES15.6', '3.12'
                    }
                    axis {
                        name 'RPM_ARCH'
                        values 'x86_64', 'aarch64'
                    }
                }
                environment {
                    DOCKER_ARCH = sh(returnStdout: true, script: "[ ${RPM_ARCH} == 'x86_64' ] && echo -n 'amd64' || echo -n 'arm64'")
                    PY_VERSION = sh(script: "echo \${DOCKER_TAG%%-*}", returnStdout: true).trim()
                    SOURCE_DOCKER_IMAGE = "${pyImage}:${DOCKER_TAG}"
                    DOCKER_BUILD_IMAGE = "${SOURCE_DOCKER_IMAGE}-${DOCKER_ARCH}"
                    RPM_NAME = getRpmName(env.NAME, env.PY_VERSION)
                }
                stages {
                    stage('Pull & tag') {
                        steps {
                            lock('docker-image-pull') {
                                sh "docker pull --platform linux/${DOCKER_ARCH} ${SOURCE_DOCKER_IMAGE}"
                                sh "docker tag ${SOURCE_DOCKER_IMAGE} ${DOCKER_BUILD_IMAGE}"
                            }
                        }
                    }
                    stage("Build") {
                        agent {
                            docker {
                                reuseNode true
                                args "-v /home/jenkins/.ssh:/home/jenkins/.ssh -v /home/jenkins/.netrc:/home/jenkins/.netrc --platform linux/${DOCKER_ARCH}"
                                image "${DOCKER_BUILD_IMAGE}"
                            }
                        }
                        environment {
                            PY_PATH = getPythonBinaryPath(env.PY_VERSION)
                            SLE_VERSION = getSleVersion()
                            RPM_OS = getSleRpmOs(env.SLE_VERSION)
                            BUILD_RELDIR = buildRelDir(env.RPM_NAME, env.RPM_OS, env.RPM_ARCH)
                        }
                        steps {
                            sh "make python_rpm"
                        }
                    }
                    stage ("Publish") {
                        agent {
                            docker {
                                reuseNode true
                                args "-v /home/jenkins/.ssh:/home/jenkins/.ssh -v /home/jenkins/.netrc:/home/jenkins/.netrc --platform linux/${DOCKER_ARCH}"
                                image "${DOCKER_BUILD_IMAGE}"
                            }
                        }
                        environment {
                            SLE_VERSION = getSleVersion()
                            RPM_OS = getSleRpmOs(env.SLE_VERSION)
                            BUILD_RELDIR = buildRelDir(env.RPM_NAME, env.RPM_OS, env.RPM_ARCH)
                        }
                        steps {
                            publishRpms(env.RPM_NAME, env.RPM_OS, env.RPM_ARCH, env.BUILD_RELDIR)
                        }
                    }
                }
            }
        }
        stage('Build meta RPMs') {
            agent {
                docker {
                    args '-v /home/jenkins/.ssh:/home/jenkins/.ssh -v /home/jenkins/.netrc:/home/jenkins/.netrc'
                    reuseNode true
                    image "${sleImage}:latest"
                }
            }
            environment {
                RPM_OS = "${env.META_RPM_OS}"
                RPM_NAME = "${env.META_RPM_NAME}"
                RPM_ARCH = "${env.META_RPM_ARCH}"
                SPEC_FILE = "${env.META_RPM_SPEC_FILE}"
                BUILD_RELDIR = "${env.META_BUILD_RELDIR}"
            }
            steps {
                sh "make meta_rpm"
            }
        }
        stage('Publish meta RPMs') {
            steps {
                // Publish meta RPMs
                publishRpms(env.META_RPM_NAME, env.META_RPM_OS, env.META_RPM_ARCH, env.META_BUILD_RELDIR)
            }
        }
    }
}
